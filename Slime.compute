// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel UpdateActor
#pragma kernel UpdateTrailsHorizontal
#pragma kernel UpdateTrailsVertical


struct Actor
{
    int species;
    float2 position;
    float angle;
};

struct Species
{
    int index;
    float sensorSize;
    float sensorAngle;
    float sensorDistance;
    float moveSpeed;
    float turnSpeed;
    float intentionalTurnWeight;
    float randomTurnWeight;
    float4 color;
    float4 inverseColor;
};

struct Settings
{
    float2 sensorSizeRange;
    float2 sensorAngleRange;
    float2 sensorDistanceRange;
    float2 moveSpeedRange;
    float2 turnSpeedRange;
    float2 intentionalTurnRange;
    float2 randomTurnRange;
    
    float angleAdjustmentWeight;
};

//parameters with no struct
int resolution;
float deltaTime;
float time;
float evaporateSpeed;

//buffers
RWStructuredBuffer<Actor> actors;
StructuredBuffer<Species> species;
StructuredBuffer<Settings> settingsBuffer;

//textures and samplers
RWTexture2D<float4> _drawTex;
RWTexture2D<float4> _trailTexToWrite;
RWTexture2D<float4> _trailTexToWrite2;

Texture2D _angleAdjustmentTexture;
SamplerState sampler_angleAdjustmentTexture;

Texture2D _trailTexToSample;
SamplerState sampler_trailTexToSample;


float PI = 3.1415926535;

//from https://www.reedbeta.com/blog/hash-functions-for-gpu-rendering/
uint pcg_hash(uint input)
{
    uint state = input * 747796405u + 2891336453u;
    uint word = ((state >> ((state >> 28u) + 4u)) ^ state) * 277803737u;
    return (word >> 22u) ^ word;
}

float Random(uint seed)
{
    return float(pcg_hash(seed)) / 4294967295.0;
}

float2 getOffset(float angle, float distance)
{
    return float2(
        cos(angle) * distance,
        sin(angle) * distance
    );
}

//samples a square of pixels centered on "position," square extends "size" pixels in all four directions
float4 sensorSample(int2 position, int size)
{
    float4 sum = float4(0, 0, 0, 0);
    int samplesTaken = 0;
    
    //start sampling at bottom left corner
    int2 currentSampleOffset = int2(-size, -size);
    while (true)
    {
        int2 currentSamplePosition = position + currentSampleOffset;
        
        //only sample within the image
        if (currentSamplePosition.y < 0 &&
            currentSamplePosition.y > resolution &&
            currentSamplePosition.x < 0 &&
            currentSamplePosition.x > resolution)
        {
            continue;
        }
        
        //sample trail texture at position
        sum += _trailTexToWrite[currentSamplePosition];
        samplesTaken++;
        
        //increment x
        currentSampleOffset.x++;
        
        //wrap around to the next row if x exceeds size
        if (currentSampleOffset.x > size)
        {
            currentSampleOffset.x = -size;
            currentSampleOffset.y++;
        }
        
        //when reaching the end of the last row, calculate the result
        if (currentSampleOffset.y > size)
        {
            float4 result = sum / samplesTaken;
            return result;
        }
    }
    
    //hopefully never reach this
    return sum;
}

[numthreads(64,1,1)]
void UpdateActor (uint3 id : SV_DispatchThreadID)
{
    //read info from buffers
    Actor a = actors[id.x];
    Species s = species[a.species];
    Settings settings = settingsBuffer[0];
    
    //calculate adjustment to angle from texture
    float2 uv = a.position / resolution;
    float4 angleAdjustment = _angleAdjustmentTexture.SampleLevel(sampler_angleAdjustmentTexture, uv, 0);
    float angleAdjustmentWeight = settings.angleAdjustmentWeight;
    
    //initialize variables from settings
    int sensorSize = (int) floor(s.sensorSize * settings.sensorSizeRange.y + settings.sensorSizeRange.x);
    float sensorAngle = (s.sensorAngle + (angleAdjustment - 0.5) * angleAdjustmentWeight) * settings.sensorAngleRange.y + settings.sensorAngleRange.x;
    float sensorDistance = s.sensorDistance * settings.sensorDistanceRange.y + settings.sensorDistanceRange.x;
    float moveSpeed = s.moveSpeed * settings.moveSpeedRange.y + settings.moveSpeedRange.x;
    float turnSpeed = s.turnSpeed * settings.turnSpeedRange.y + settings.turnSpeedRange.x;
    float intentionalTurnWeight = s.intentionalTurnWeight * settings.intentionalTurnRange.y + settings.intentionalTurnRange.x;
    float randomTurnWeight = s.randomTurnWeight * settings.randomTurnRange.y + settings.randomTurnRange.x;
    
    //get ready to sample trail texture to find members of own species
    float2 centerSensorPos = getOffset(a.angle, sensorDistance) + a.position;
    float2 leftSensorPos = getOffset(a.angle + sensorAngle, sensorDistance) + a.position;
    float2 rightSensorPos = getOffset(a.angle - sensorAngle, sensorDistance) + a.position;
    
    //sample trail texture at three points
    float4 centerSensorOutput = sensorSample(centerSensorPos, sensorSize);
    float centerValue = dot(centerSensorOutput * s.color, float4(1, 1, 1, 1)) - dot(centerSensorOutput * s.inverseColor, float4(1,1,1,1))/2;
    float4 leftSensorOutput = sensorSample(leftSensorPos, sensorSize);
    float leftValue = dot(leftSensorOutput * s.color, float4(1, 1, 1, 1)) - dot(leftSensorOutput * s.inverseColor, float4(1, 1, 1, 1))/2;
    float4 rightSensorOutput = sensorSample(rightSensorPos, sensorSize);
    float rightValue = dot(rightSensorOutput * s.color, float4(1, 1, 1, 1)) - dot(rightSensorOutput * s.inverseColor, float4(1, 1, 1, 1))/2;
    
    //decide which way to turn based on samples
    uint seed = (uint) id.x + a.position.x + a.position.y + time;    
    if (leftValue >= centerValue && leftValue > rightValue)
    {
        a.angle += turnSpeed * deltaTime * Random(seed) * intentionalTurnWeight;
    }
    else if (rightValue >= centerValue && rightValue > leftValue)
    {
        a.angle -= turnSpeed * deltaTime * Random(seed) * intentionalTurnWeight;
    }
    else
    {
        //Random is between 0 and 1, so remove a half and double to get -1 to 1
        a.angle += (Random(seed) - 0.5) * 2 * randomTurnWeight * turnSpeed * deltaTime;
    }
    
    //update position based on speed and angle
    float2 movement = getOffset(a.angle, moveSpeed * deltaTime);
    a.position = a.position + movement;
    
    //keep position within texture bounds
    if (a.position.x > resolution || a.position.x < 0 || a.position.y > resolution || a.position.y < 0)
    {
        a.angle = (Random(seed + id.x + time)) * 360;
    }
    
    //calculate force keeping Actors within a circle
    float2 center = float2(resolution / 2., resolution / 2.);
    float radiusSquared = (resolution / 2) * (resolution / 2);
    float distanceSquaredFromCenter = dot(a.position - center, a.position - center);
    
    float turnBackDistance = max(distanceSquaredFromCenter - radiusSquared, 0);
    
    float turnBackDirection = saturate(max(dot(a.position - center, movement), 0));
    
    float turnBackWeight = 0.05;
    
    a.angle += (turnBackWeight * turnBackDirection * turnBackDistance) * turnSpeed * deltaTime;
    
    
    //final clamp of position just in case
    a.position = clamp(a.position, float2(0, 0), float2(resolution, resolution));
    int2 texturePosition = int2(floor(a.position.x), floor(a.position.y));
    
    //return actor info to the buffer
    actors[id.x] = a;
       
    //write actor color to the trail texture
    _trailTexToWrite[texturePosition] += s.color;
    _trailTexToWrite2[texturePosition] += s.color; 
}

[numthreads(8, 8, 1)]
void UpdateTrailsHorizontal (uint3 id : SV_DispatchThreadID)
{    
    float otherKernel[7] =
    {
        0.001,
        0.01,
        0.2,
        4.,
        0.2,
        0.01,
        0.001
    };
    
    float4 sum = float4(0, 0, 0, 0);
    float maxValue = 0;
    
    //sum adjacent pixels, weighted by the kernel
    for (int x = 0; x < 7; x++)
    {
        maxValue += otherKernel[x];
        float2 uv = float2(id.x + 0.5 + (x - 3), id.y + 0.5) / (float) resolution;
        sum += (_trailTexToSample.SampleLevel(sampler_trailTexToSample, uv, 0)) * otherKernel[x];
    }
    
    //divide by the maximum potential value to get the average
    float4 col = sum / maxValue;
    
    _trailTexToWrite[id.xy] = col;
}

[numthreads(8, 8, 1)]
void UpdateTrailsVertical(uint3 id : SV_DispatchThreadID)
{
    float otherKernel[7] =
    {
        0.001,
        0.01,
        0.2,
        4.,
        0.2,
        0.01,
        0.001
    };
    
    float4 sum = float4(0, 0, 0, 0);
    float maxValue = 0;
    
    //sum adjacent pixels weighted by the kernel
    for (int y = 0; y < 7; y++)
    {
        maxValue += otherKernel[y];
        float2 uv = float2(id.x + 0.5, id.y + (y - 3) + 0.5) / (float) resolution;
        sum += (_trailTexToSample.SampleLevel(sampler_trailTexToSample, uv, 0)) * otherKernel[y];
    }
    
    //divide by the maximum potential value to get the average
    float4 col = sum / maxValue;
    
    
    //this pass is called second so it also handles the evaporation
    col -= ((1. / evaporateSpeed) * deltaTime);
    
    //set alpha back to 1
    col.a = 1.;
    
    _trailTexToWrite[id.xy] = col;
}


